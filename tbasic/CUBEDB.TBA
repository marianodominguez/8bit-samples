10 DIM X(25): DIM Y(25):DIM Z(25):DIM TCOS(360):DIM TSIN(360):SQ2=SQR(2):SQ6=SQR(6)
15 PI=3.1415926535:W=159:H=95:SCL=25
20 PRINT "PRE-CALCULATING COS AND SIN"
25 F.I=1 TO 4*6: READ X,Y,Z: X(I)=X:Y(I)=Y:Z(I)=Z:N.I
30 REM PRECALCULATE COS AND SIN
35 IDX=1:DEG
40 FOR I=0 TO 360 STEP 5
45     TCOS(IDX) = COS(I)
50     TSIN(IDX) = SIN(I)
55     PRINT ".";
60    IDX=IDX+1
65 NEXT I
67 NP=IDX
70 RT=PEEK(106)-24:GRAPHICS 6+16
80 POKE 88,0:POKE 89,RT-8:? #6;CHR$(125):POKE 106,RT:GRAPHICS 6+16:PAGE=16
90 POKE 88,0:POKE 89,RT-16:GRAPHICS 6+16
100 DL=PEEK(560)+256*PEEK(561):DL4=DL+4:DL5=DL+5:POKE DL4,0:POKE DL5,RT+4
110 POKE DL5,RT:REM HIDE DRAWING PAGE
120 FOR TH=1 TO NP
130 IDX=1
140 POKE 88,0:POKE 89,RT+PAGE:GOSUB 500
150 SETCOLOR 0,8,10:COLOR 1
160 FOR I=1 TO 5
170 FOR J=1 TO 4
180 X=X(IDX):Y=Y(IDX):Z=Z(IDX)
190 IDX=IDX+1
200 REM ROTATION
210 YR = Y*TCOS(TH) - Z*TSIN(TH)
220 ZR = Y*TSIN(TH) + Z*TCOS(TH)
230 XP=(X-ZR)/SQ2
240 YP=(X+2*YR+ZR)/SQ6
250 XS = XP*SCL+W/2
260 YS = (H/2)-YP*SCL
270 IF J=1
280 X0=XS:Y0=YS
290 PLOT XS,YS
300 ELSE
310 PLOT X1,Y1:DRAWTO XS,YS
320 ENDIF
330 X1=XS:Y1=YS
340 NEXT J
350 DRAWTO X0,Y0
360 NEXT I
370 POKE DL5,RT+PAGE
380 IF PAGE=8:PAGE=16:ELSE:PAGE=8:ENDIF 
390 NEXT TH
400 GOTO 120
450 END
500 REM FAST SCREEN CLEAN
510 ADDR=PEEK(88)+256*PEEK(89)
520 MEM=(RT)*256
530 MOVE MEM,ADDR,2174
540 RETURN
1000 DATA -1,-1,-1,-1, 1,-1,-1, 1, 1,-1,-1, 1,1,-1,-1,1, 1,-1,1, 1, 1,1,-1, 1
1010 DATA -1,-1,-1,-1,-1, 1,1,-1, 1,1,-1,-1,1,-1, 1,-1,-1, 1,-1, 1, 1,1, 1, 1
1020 DATA 1, 1, 1, 1, 1,-1,-1, 1,-1,1, 1,-1,-1,-1,-1, 1,-1,-1,1, 1,-1,-1, 1,-1
