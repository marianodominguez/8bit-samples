10 DIM x(25): DIM y(25):DIM z(25):DIM tcos(360):DIM tsin(360):sq2=SQR(2):sq6=SQR(6)
15 W=159:H=95:scl=25:STP=5
20 PR. "Pre-calculating cos and sin"
25 FOR i=1 TO 4*6: READ x,y,z: x(i)=x:y(i)=y:z(i)=z:N.i
30 REM precalculate cos and sin
35 idx=1:DEG
40 FOR i=0 TO 90 STEP STP
45     tcos(idx) = COS(i)
50     tsin(idx) = SIN(i)
55     print ".";
60    idx=idx+1
65 NEXT i
67 NP=idx:CLS
100 DIM VX(20*NP):DIM VY(20*NP)
120 FOR TH=1 TO NP
125 idx=1
130 FOR i=1 TO 5
140 FOR j=1 TO 4
160 x=x(idx):y=y(idx):z=z(idx)
180 REM rotation
190 yr = y*tcos(TH) - z*tsin(TH)
200 zr = y*tsin(TH) + z*tcos(TH)
210 xp=(x-zr)/sq2
220 yp=(x+2*yr+zr)/sq6
230 xs = xp*scl+W/2
240 ys = (H/2)-yp*scl
250 VX(idx*TH)=xs:VY(idx*TH)=ys
255 idx=idx+1
260 NEXT j
270 NEXT i
275 POS.10,10:? (idx-1)*TH;"/";NP*20;
280 NEXT TH
400 idx=1
410 RT=PEEK(106)-24:GRAPHICS 6+16
420 POKE 88,0:POKE 89,RT-8:? #6;CHR$(125):POKE 106,RT:GRAPHICS 6+16:PAGE=16
430 POKE 88,0:POKE 89,RT-16:GRAPHICS 6+16
440 DL=PEEK(560)+256*PEEK(561):DL4=DL+4:DL5=DL+5:POKE DL4,0:POKE DL5,RT+4
450 POKE DL5,RT:REM hide drawing page
460 FOR TH=1 TO NP
465 idx=1
470 POKE 88,0:POKE 89,RT+PAGE:GOSUB 800
480 SETCOLOR 0,8,10:COLOR 1
490 FOR i=1 TO 5
500 FOR j=1 TO 4
510 xs=VX(idx*TH):ys=VY(idx*TH)
520 idx=idx+1
530 IF j=1
540 x0=xs:y0=ys
550 PLOT xs,ys
560 ELSE
570 PLOT x1,y1:DRAWTO xs,ys
580 ENDIF
590 x1=xs:y1=ys
600 NEXT j
610 DRAWTO x0,y0
620 NEXT i
630 POKE DL5,RT+PAGE
640 IF PAGE=8:PAGE=16:ELSE:PAGE=8:ENDIF
650 NEXT TH
660 GOTO 400
670 END
800 REM FAST SCREEN CLEAN
810 ADDR=PEEK(88)+256*PEEK(89)
820 MEM=(RT)*256
830 MOVE MEM,ADDR,2174
840 RETURN
1000 DATA -1,-1,-1,-1, 1,-1,-1, 1, 1,-1,-1, 1,1,-1,-1,1, 1,-1,1, 1, 1,1,-1, 1
1010 DATA -1,-1,-1,-1,-1, 1,1,-1, 1,1,-1,-1,1,-1, 1,-1,-1, 1,-1, 1, 1,1, 1, 1
1020 DATA 1, 1, 1, 1, 1,-1,-1, 1,-1,1, 1,-1,-1,-1,-1, 1,-1,-1,1, 1,-1,-1, 1,-1
