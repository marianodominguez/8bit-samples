100 REM Basic Month 4 : Lunar Corps Atari 1.0
110 REM original AppleII bt FozzTech
120 REM Atari version By papa_robot
125 GRAPHICS 0
126 REM Reserve memory for sprites, these have to be the first variables
130 DIM FIL$(1), FIL2$((INT(ADR(FIL$)/1024)+1)*2048-ADR(FIL$)-1)
132 DIM BUFFER$(768), MISSILES$(256), PLAYER0$(256), PLAYER1$(256), PLAYER2$(256), PLAYER3$(256)
133 H=33:DIM IMAGE$(H), RKT1$(H),RKT2$(H),RKT3$(H),RKT4$(H),FR1$(H),FR2$(H): REM H is sprite height
135 DIM C1$(1),C2$(1),C3$(1),C4$(1),C5$(1),C6$(1),P$(1),T$(2)
136 DIM S$(5),SP$(255)
200 REM Read sprites to memory
210 FRAMES=2:EXEC RDATA:RKT1$=IMAGE$:EXEC RDATA:RKT2$=IMAGE$
212 EXEC RDATA:RKT3$=IMAGE$:EXEC RDATA:RKT4$=IMAGE$
215 SETCOLOR 2,0,0:SETCOLOR 1,1,14
220 DIM PEAK$(4):PEAK$=" /\_"
230 COLS=39:ROWS=23:SIZ=COLS+4
235 DIM L1$(SIZ),L2$(SIZ),BL$(SIZ),SP$(SIZ),SE$(10),LM$(10),ML$(10),LE$(10)
300 EXEC DRAWSF
310 YS=SQR(2)*SQR(ROWS-TL+ZL)-1
320 XE=INT((ZE-ZS)/2)+ZS
330 XV=XE/YS:YV=0
340 X=1:Y=1
350 EXEC SETUP_PM
420 FOR I=1 TO YS
430   FR1$=RKT1$:FR2$=RKT2$:EXEC DRAW
440   YV=YV+1
450   X=X+XV:Y=Y+YV
460   FR1$=RKT1$:FR2$=RKT2$:IF I>=YS-2 THEN FR1$=RKT3$:FR2$=RKT4$
470   EXEC DRAW
480   FOR J=1 TO 400:NEXT J
490 NEXT I
500 SP$=LE$:EXEC DRAW
510 X=(ZE-ZS)/2+ZS-1:Y=ROWS-TL+ZL-1
520 SP$=LM$:EXEC DRAW
530 SP$="The Eagle has landed":X=(COLS-LEN(SP$))/2:Y=ROWS/2:EXEC TEX
590 SP$="19-Jul-2019":X=(COLS-LEN(SP$))/2:Y=2:EXEC TEX
600 SP$="/u/papa_robot":X=(COLS-LEN(SP$))/2:Y=3:EXEC TEX
890 IF PEEK($D01F)<>6 THEN GOTO 890
900 GRAPHICS 0
999 END

1000 PROC SETUP_PM
1010   POKE 54279,ADR(BUFFER$)/256:POKE 559,46:POKE 53277,3:POKE 704,114:POKE 705,114:POKE 559,62:POKE 706,2
1020   POKE 707,38
1030 ENDPROC

1499 REM Draw text in SP$ at X,Y
1500 PROC TEX
1510   IF Y<1 OR Y>ROWS OR X<1 OR X>COLS THEN ENDPROC
1520 REM Set the left margin to the X position
1530 POSITION X,Y:PRINT :PRINT SP$;
1550 ENDPROC
1599 REM real sprite at position
1600 PROC DRAW: REM X,Y position
1610  POKE 53248,X:POKE 53249,X+8
1620  PLAYER0$(Y)=FR1$:PLAYER1$(Y)=FR2$
1690 ENDPROC
1699 REM READ sprite data
1700 PROC RDATA
1710   FOR I=1 TO H:READ D:IMAGE$(I,I)=CHR$(D):NEXT I
1750 ENDPROC

2500 REM Draw lunar surface
2501 PROC DRAWSF
2510   DIDUP=0:L1$="":FOR I=1 TO COLS+4:L1$(LEN(L1$)+1)=" ":NEXT I
2520   ZS=0:ZE=0:TL=0
2530   L2$="  ":UP=DIDUP:IF NOT UP AND L1$(3,3)<>" " THEN UP=1
2540   IF NOT DIDUP AND UP THEN DIDUP=UP
2550   Z1=0:Z2=0
2560   FOR I=3 TO COLS+2:PRINT ".";CHR$(30);
2562     REM BUG IN APPLE ?
2565     REM CHR8 left Arrow
2567     IF LEN(L1$)<I+2 THEN L1$(I+2,I+2)=" "
2570     C1$=L2$(I-2,I-2):C2$=L2$(I-1,I-1)
2580     C3$=L1$(I-1,I-1):C4$=L1$(I,I)
2590     C5$=L1$(I+1,I+1):C6$=L1$(I+2,I+2)
2600     IF PEAK=0 AND L1$(I-1,I+2)="    " THEN PEAK=INT(RND*3):PEAK=PEAK+(PEAK=2):PEAK=PEAK*INT(RND*2)
2610     IF C2$="/" AND C4$=" " THEN PEAK=2
2620     IF (C1$="_" OR C1$="\") AND C2$=" " THEN PEAK=0
2630     IF C4$<>" " THEN PEAK=0
2640     IF (C3$="\" OR C3$="_") AND C4$=" " THEN PEAK=2
2650     IF C2$="_" AND C6$="/" THEN PEAK=3
2660     IF (C2$="\" OR C2$=" " OR C2$="_") AND C4$=" " AND (C5$="_" OR C5$="/") THEN PEAK=1
2670     IF C2$="\" AND C6$<>" " THEN PEAK=3
2680     IF (C5$="/" OR C5$="_") THEN PEAK=1
2690     IF C4$<>" " THEN PEAK=0
2700     IF UP AND PEAK<>2 THEN PEAK=0
2710     IF PEAK=1 THEN UP=1
2720     IF PEAK=2 THEN UP=0
2730     P$=PEAK$(PEAK+1,PEAK+1):PRINT P$;:L2$(LEN(L2$)+1)=P$
2740     IF PEAK=3 AND NOT Z1 THEN Z1=I
2750     IF PEAK<>3 AND C2$="_" THEN Z2=I
2760     IF Z1 AND Z2 THEN Z3=Z1:Z4=Z2:Z1=0:Z2=0:IF Z4-Z3>=3 AND Z3>ZS THEN ZS=Z3:ZE=Z4:ZL=TL
2770     IF PEAK<>3 OR (C1$="_" AND C2$="_") THEN PEAK=0
2780   NEXT I
2790   L1$=L2$
2800   C2=0
2810   FOR I=3 TO COLS+2
2820     IF L1$(I,I)=" " THEN C2=C2+1
2830   NEXT I
2840   TL=TL+1
2850   IF C2<COLS THEN PRINT :GOTO 2530
2860   ZS=ZS-3:ZE=ZE-3
2861 ENDPROC
9000 REM rocket data
10110 REM **** FRAME 1
10120 DATA 0,3,6,6,4,4,7,14
10130 DATA 10,12,10,15,11,31,61,123
10140 DATA 121,127,111,102,70,6,0,0
10150 DATA 0,0,0,0,0,0,0,0
10160 DATA 0
10170 REM **** FRAME 2
10180 DATA 0,3,6,6,4,4,7,14
10190 DATA 10,12,10,15,11,31,61,123
10200 DATA 121,127,111,102,70,6,0,6
10210 DATA 1,2,1,0,1,1,0,0
10220 DATA 0
10230 REM **** P1 DATA ****
10240 REM **** FRAME 1
10250 DATA 0,192,96,96,32,32,224,176
10260 DATA 144,112,144,240,208,248,252,222
10270 DATA 158,254,254,110,102,98,0,0
10280 DATA 0,0,0,0,0,0,0,0
10290 DATA 0
10300 REM **** FRAME 2
10310 DATA 0,192,96,96,32,32,224,176
10320 DATA 144,112,144,240,208,248,252,222
10330 DATA 158,254,254,110,102,98,0,224
10340 DATA 192,0,128,192,128,64,128,0
10350 DATA 0
