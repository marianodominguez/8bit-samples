100 REM Basic Month 4 : Lunar Corps Atari 1.0
110 REM original AppleII bt FozzTech
120 REM Atari version By papa_robot
130 GRAPHICS 0
140 REM Reserve memory for sprites, these have to be the first variables
150 DIM FIL$(1),FIL2$((INT(ADR(FIL$)/1024)+1)*2048-ADR(FIL$)-1)
160 DIM BUFFER$(768),MISSILES$(256),PLAYER0$(256),PLAYER1$(256),PLAYER2$(256),PLAYER3$(256)
170 H=33:DIM IMAGE$(H),RKT1$(H),RKT2$(H),RKT3$(H),RKT4$(H),FR1$(H),FR2$(H):REM H is sprite height
180 DIM C1$(1),C2$(1),C3$(1),C4$(1),C5$(1),C6$(1),P$(1),T$(2)
190 DIM S$(5),SP$(80),BLANK$(256):BLANK$=CHR$(0):BLANK$(255)=CHR$(0):BLANK$(2)=BLANK$
200 REM Read sprites to memory
210 EXEC RDATA:RKT1$=IMAGE$:EXEC RDATA:RKT3$=IMAGE$
220 EXEC RDATA:RKT2$=IMAGE$:EXEC RDATA:RKT4$=IMAGE$
230 SETCOLOR 2,0,0:SETCOLOR 1,1,14:POSITION 0,23:POKE 82,0:POKE 752,1
240 DIM PEAK$(4):PEAK$=" /\_"
250 COLS=39:ROWS=23:SIZ=COLS+4
260 DIM L1$(SIZ),L2$(SIZ),BL$(SIZ)
270 EXEC DRAWSF
280 YS=SQR(2)*SQR(ROWS-TL+ZL)-1
290 XE=INT((ZE-ZS)/2)+ZS
300 XV=XE/YS:YV=0
310 X=1:Y=1
320 EXEC SETUP_PM
330 FOR I=1 TO YS
340   FR1$=RKT1$:FR2$=RKT2$:EXEC DRAW
350   YV=YV+1
360   X=X+XV:Y=Y+YV
370   FR1$=RKT1$:FR2$=RKT2$:IF I>=YS-2 THEN FR1$=RKT3$:FR2$=RKT4$
380   EXEC DRAW
390   FOR J=1 TO 400:NEXT J
400 NEXT I
410 FR1$=RKT1$:FR2$=RKT2$:EXEC DRAW
420 X=(ZE-ZS)/2+ZS-1:Y=ROWS-TL+ZL-1
430 FR1$=RKT1$:FR2$=RKT2$:EXEC DRAW
440 SP$="The Eagle has landed":X=(COLS-LEN(SP$))/2:Y=ROWS/2:EXEC TEX
450 SP$="19-Jul-2019":X=(COLS-LEN(SP$))/2:Y=2:EXEC TEX
460 SP$="/u/papa_robot":X=(COLS-LEN(SP$))/2:Y=3:EXEC TEX
465 SP$="PRESS START":X=(COLS-LEN(SP$))/2:Y=5:EXEC TEX
470 IF PEEK($D01F)<>6 THEN GOTO 470
480 POKE 53277,0:GRAPHICS 0
490 END 
500 PROC SETUP_PM
510   POKE 54279,ADR(BUFFER$)/256:POKE 559,46:POKE 53277,3:POKE 704,$24:POKE 705,$24:POKE 559,62
520   POKE 707,38
530 ENDPROC 
540 REM Draw text in SP$ at X,Y
550 PROC TEX
560   IF Y<1 OR Y>ROWS OR X<1 OR X>COLS THEN ENDPROC 
570 REM Set the left margin to the X position
580 POSITION X,Y:PRINT SP$;
590 ENDPROC 
600 REM real sprite at position
610 PROC DRAW:REM X,Y position
615   XSCALE=152/COLS:YSCALE=230/ROWS
620   POKE 53248,INT(X*XSCALE)+48:POKE 53249,(INT(X*XSCALE))+48+8
625   PLAYER0$=BLANK$:PLAYER1$=BLANK$
630   PLAYER0$(INT(Y*YSCALE))=FR1$:PLAYER1$(INT(Y*YSCALE))=FR2$
640 ENDPROC 
650 REM READ sprite data
660 PROC RDATA
665   IMAGE$=CHR$(0):IMAGE$(H)=CHR$(0):IMAGE$(2)=IMAGE$
670   FOR I=1 TO H:READ D:IMAGE$(I,I)=CHR$(D):NEXT I
680 ENDPROC 
690 REM Draw lunar surface
700 PROC DRAWSF
710   DIDUP=0:L1$="":FOR I=1 TO COLS+4:L1$(LEN(L1$)+1)=" ":NEXT I
720   ZS=0:ZE=0:TL=0
730   L2$="  ":UP=DIDUP:IF NOT UP AND L1$(3,3)<>" " THEN UP=1
740   IF NOT DIDUP AND UP THEN DIDUP=UP
750   Z1=0:Z2=0
760   FOR I=3 TO COLS+2:PRINT ".";CHR$(30);
770     REM BUG IN APPLE ?
780     REM CHR8 left Arrow
790     IF LEN(L1$)<I+2 THEN L1$(I+2,I+2)=" "
800     C1$=L2$(I-2,I-2):C2$=L2$(I-1,I-1)
810     C3$=L1$(I-1,I-1):C4$=L1$(I,I)
820     C5$=L1$(I+1,I+1):C6$=L1$(I+2,I+2)
830     IF PEAK=0 AND L1$(I-1,I+2)="    " THEN PEAK=INT(RND*3):PEAK=PEAK+(PEAK=2):PEAK=PEAK*INT(RND*2)
840     IF C2$="/" AND C4$=" " THEN PEAK=2
850     IF (C1$="_" OR C1$="\") AND C2$=" " THEN PEAK=0
860     IF C4$<>" " THEN PEAK=0
870     IF (C3$="\" OR C3$="_") AND C4$=" " THEN PEAK=2
880     IF C2$="_" AND C6$="/" THEN PEAK=3
890     IF (C2$="\" OR C2$=" " OR C2$="_") AND C4$=" " AND (C5$="_" OR C5$="/") THEN PEAK=1
900     IF C2$="\" AND C6$<>" " THEN PEAK=3
910     IF (C5$="/" OR C5$="_") THEN PEAK=1
920     IF C4$<>" " THEN PEAK=0
930     IF UP AND PEAK<>2 THEN PEAK=0
940     IF PEAK=1 THEN UP=1
950     IF PEAK=2 THEN UP=0
960     P$=PEAK$(PEAK+1,PEAK+1):PRINT P$;:L2$(LEN(L2$)+1)=P$
970     IF PEAK=3 AND NOT Z1 THEN Z1=I
980     IF PEAK<>3 AND C2$="_" THEN Z2=I
990     IF Z1 AND Z2 THEN Z3=Z1:Z4=Z2:Z1=0:Z2=0:IF Z4-Z3>=3 AND Z3>ZS THEN ZS=Z3:ZE=Z4:ZL=TL
1000     IF PEAK<>3 OR (C1$="_" AND C2$="_") THEN PEAK=0
1010   NEXT I
1020   L1$=L2$
1030   C2=0
1040   FOR I=3 TO COLS+2
1050     IF L1$(I,I)=" " THEN C2=C2+1
1060   NEXT I
1070   TL=TL+1
1080   IF C2<COLS THEN PRINT :GOTO 730
1090   ZS=ZS-3:ZE=ZE-3
1100 ENDPROC 
1110 REM rocket data
1120 REM **** FRAME 1
1130 DATA 0,3,6,6,4,4,7,14
1140 DATA 10,12,10,15,11,31,61,123
1150 DATA 121,127,111,102,70,6,0,0
1160 DATA 0,0,0,0,0,0,0,0
1170 DATA 0
1190 DATA 0,3,6,6,4,4,7,14
1200 DATA 10,12,10,15,11,31,61,123
1210 DATA 121,127,111,102,70,6,0,6
1220 DATA 1,2,1,0,1,1,0,0
1230 DATA 0
1240 REM **** P1 DATA ****
1250 REM ****
1260 DATA 0,192,96,96,32,32,224,176
1270 DATA 144,112,144,240,208,248,252,222
1280 DATA 158,254,254,110,102,98,0,0
1290 DATA 0,0,0,0,0,0,0,0
1300 DATA 0
1310 REM ****
1320 DATA 0,192,96,96,32,32,224,176
1330 DATA 144,112,144,240,208,248,252,222
1340 DATA 158,254,254,110,102,98,0,224
1350 DATA 192,0,128,192,128,64,128,0
1360 DATA 0
