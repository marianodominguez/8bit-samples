10 REM DEMO OF GTIA COLORS IN GRAPHICS MODE 0
15 REM MOVE TOP MEMORY DOWNWARD TO FIT CHARACTER SET
20 POKE 106,PEEK(106)-4
30 CB=PEEK(106):REM NEW CHARACTER SET LOCATION-HI BYTE
40 GRAPHICS 0
50 CHRSET=CB*256:REM ACTUAL RAM LOCATION OF CHARACTER SET
55 REM SET UP ARRAY OF SPECIAL CHARACTER VALUES
60 DIM CT(16)
70 FOR I=0 TO 15:CT(I)=17*I:NEXT I
80 REM WRITE GTIA COLOR CHARACTERS (0-15) IN NEW CHARACTER SET
90 FOR I=0 TO 15
100 FOR J=0 TO 7
110 POKE CHRSET+I*8+J,CT(I)
120 NEXT J:NEXT I
140 REM FIND SCREEN MEMORY & WRITE CHARACTERS
141 SCREEN=PEEK(88)+PEEK(89)*256
150 FOR I=0 TO 15:POKE SCREEN+I,I:NEXT I
160 POKE 756,CB:REM NEW CHARACTER SET LOCATION - HI BYTE
170 POKE 623,192:REM TURN ON GTIA MODE 11 (16 COLOR)
180 POKE 712,8:REM CONTROLS LUMINANCE
190 GOTO 190
