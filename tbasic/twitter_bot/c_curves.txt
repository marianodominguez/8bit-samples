100 ------------------------------
110 REM GRAPHICS 8 CURVES   --
120 ------------------------------
130 EXEC INIT
140 QUIT=0
150 WHILE NOT QUIT
160   EXEC DRAW_CURVE
170   EXEC PAUSE
190 WEND 
200 END 
210 ------------------------------
220 PROC INIT
230   GRAPHICS 8+16
240   MAX_X=319:MAX_Y=191
250   SETCOLOR 2,0,0:SETCOLOR 1,1,14
260   COLOR 1
270   PI=3.14159265
280 ENDPROC 
290 PROC DRAW_CURVE
300   FOR I=0 TO 360 STEP 3
310     TH=I*(PI/180)
320     ON SHAPE EXEC FN_CONO,FN_RECTA,FN_SUN,FN_END
330     Y=50*COS(TH)
340     XS=X+160:YS=96-Y
345     X1S=X1+160:Y1S=96-Y1
350     IF XS>=0 AND XS<MAX_X AND YS>=0 AND YS<MAX_Y
360       PLOT X1S,Y1S:DRAWTO XS,YS
370     ENDIF 
380   NEXT I
390 ENDPROC 
400 PROC PAUSE
410   IF PEEK($D01F)<>6 THEN 410
420   SHAPE=SHAPE+1
430 ENDPROC 
500 PROC FN_CONO
520   X=50*SIN(TH)
530   Y=50*COS(TH)
535   X1=0
536   Y1=0
550 ENDPROC 
600 PROC FN_RECTA
620   X=50*SIN(TH)
630   Y=50*COS(TH)
640   X1=80*SIN(TH)
650   Y1=80*SIN(TH)
700 ENDPROC 
800 PROC FN_SUN
820   X=50*SIN(TH)
830   Y=50*COS(TH)
840   X1=80*SIN(TH)
850   Y1=80*COS(TH)
900 ENDPROC 
1000 PROC FN_END
1010   QUIT=1
1020 ENDPROC 
