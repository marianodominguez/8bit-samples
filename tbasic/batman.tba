10 GRAPHICS 8+16:SETCOLOR 2,0,10:SETCOLOR 1,0,0:COLOR 1
20 FOR X=-7 TO 7 STEP 0.08
30   AR=1-(X/7)*(X/7):EXEC SQRT:W=3*SQR(RE)
40   AR=4-(X+1)*(X+1):EXEC SQRT:L=(6/7)*SQR(10)+(3+X)/2-3/7*SQR(10)*RE
50   H=(3*(ABS(X-1/2)+ABS(X+1/2)+6)-11*(ABS(X-3/4)+ABS(X+3/4)))/2
60   R=6/7*SQR(10)+(3-X)/2-3/7*SQR(10)*SQR(ABS(4-(X-1)*(X-1)))
70   XX=X+1:EXEC HS:HS1=V:XX=X-1:EXEC HS:HS2=V:XX=X+3:EXEC HS:HS3=V:XX=X-3:EXEC HS:HS4=V
80   YT=(H-L)*HS1+(R-H)*HS2+(L-W)*HS3+(W-R)*HS4+W
90   AR=1-(ABS(ABS(X)-2)-1):EXEC SQRT:RS1=RE
100   AR=1-(X/7)*(X/7)-3:EXEC SQRT:A=ABS(X/2)+RS1*(ABS(ABS(X)-2)-1)-(3*SQR(33)-7)/112*X*X+3*RE
110   AR=1-(X/7)*(X/7):EXEC SQRT:YB=(1/2)*A*(SGN(X+4)-SGN(X-4))-3*RE
120   PLOT 12*X+150,96-12*YT
130   DRAWTO 12*X+150,96-12*YB
140 NEXT X
150 GOTO 150
160 PROC HS
170   V=1
180   IF XX=0 THEN V=1/2
190   IF XX<0 THEN V=0
200 ENDPROC 
210 PROC SQRT
220   RE=0
230   IF AR>0 THEN RE=SQR(AR)
240 ENDPROC 
